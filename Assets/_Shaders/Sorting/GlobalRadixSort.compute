#pragma use_dxc
#pragma kernel GlobalRadixSort

#include <Assets/_Shaders/Constants.cginc>

StructuredBuffer<TKey> sortedBlocksKeysData; // size = THREADS_PER_BLOCK * BLOCK_SIZE
StructuredBuffer<uint> sortedBlocksValuesData; // size = THREADS_PER_BLOCK * BLOCK_SIZE

StructuredBuffer<uint> offsetsData; // size = BLOCK_SIZE * BUCKET_SIZE
StructuredBuffer<uint> sizesData; // size = BLOCK_SIZE * BUCKET_SIZE

RWStructuredBuffer<TKey> sortedKeysData; // size = THREADS_PER_BLOCK * BLOCK_SIZE
RWStructuredBuffer<uint> sortedValuesData; // size = THREADS_PER_BLOCK * BLOCK_SIZE

uniform int bitOffset;

groupshared uint offsetsTile[BUCKET_SIZE];
groupshared uint sizesTile[BUCKET_SIZE];

[numthreads(THREADS_PER_BLOCK,1,1)]
void GlobalRadixSort(uint3 tid : SV_GroupThreadID, uint3 gid : SV_GroupID)
{
    const uint threadId = tid.x;
    const uint groupId = gid.x;

    const TKey key = sortedBlocksKeysData[groupId * THREADS_PER_BLOCK + threadId];
    const uint value = sortedBlocksValuesData[groupId * THREADS_PER_BLOCK + threadId];
    if (threadId < BUCKET_SIZE)
    {
        offsetsTile[threadId] = offsetsData[groupId * BUCKET_SIZE + threadId];
        sizesTile[threadId] = sizesData[groupId + threadId * BLOCK_SIZE];
    }
    AllMemoryBarrierWithGroupSync();

    const TKey radix = (key >> bitOffset) & (BUCKET_SIZE - 1);
    const uint indexOutput = sizesTile[radix] + threadId - offsetsTile[radix];
    
    sortedKeysData[indexOutput] = key;
    sortedValuesData[indexOutput] = value;
}
